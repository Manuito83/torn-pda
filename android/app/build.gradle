plugins {
    id "com.android.application"
    id "kotlin-android"
    id "org.jetbrains.kotlin.plugin.compose"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
def flutterVersionName = localProperties.getProperty('flutter.versionName')

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 36

    namespace 'com.manuito.tornpda'

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.manuito.tornpda"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 36 // See android/build.gradle when updating as well, for packages bug fix (if still needed)
        multiDexEnabled true
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // Filter for architectures supported by Flutter.
            // https://github.com/flutter/flutter/issues/83596#issuecomment-1122132782
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Align Java compile target with Kotlin toolchain (JDK 21)
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
    }

}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '21'
    }
}

flutter {
    source '../..'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation platform('androidx.compose:compose-bom:2024.10.00')
    implementation 'androidx.compose.runtime:runtime'
    
    // Required by Home Widget
    implementation "androidx.work:work-runtime-ktx:2.8.1"

    // See https://github.com/flutter/flutter/issues/110658 (local_notifications_plugin)
    implementation 'androidx.window:window:1.0.0'
    implementation 'androidx.window:window-java:1.0.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}

// TODO DELETE v3.8.4 to make it compatible with WorkManager >= 0.9.0
/*
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'androidx.work' && 
            (details.requested.name == 'work-runtime' || details.requested.name == 'work-runtime-ktx')) {
            details.useVersion '2.7.1'
        }
    }
}
*/
